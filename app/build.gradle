plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def keyProperties = new Properties()
try {
    keyProperties.load(new FileInputStream(rootProject.file("key.properties")))
} catch (Exception ignored) {
    logger.warn("No key.properties file found - revering to defaults!")
}
def testDeviceIds = keyProperties['testDeviceIds'] ?: "{}"

android {
    namespace "com.softwareoverflow.maxigriphangboardtrainer"
    compileSdk 34

    defaultConfig {
        applicationId "com.softwareoverflow.maxigriphangboardtrainer"
        minSdk 26
        targetSdk 34
        versionCode 6
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String[]", "DEV_DEVICES", testDeviceIds)
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "Version ${defaultConfig.versionName}${versionNameSuffix}"
            resValue "string", "ad_id_banner", "\"ca-app-pub-3940256099942544/6300978111\""
            resValue "string", "ad_id_workout_start", "\"ca-app-pub-3940256099942544/1033173712\""
            resValue "string", "ad_id_workout_end", "\"ca-app-pub-3940256099942544/1033173712\""
        }

        release {
            minifyEnabled true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )

            resValue "string", "app_version", "Version ${defaultConfig.versionName}"

            resValue "string", "ad_id_banner", "\"ca-app-pub-5961771507160254/3105386278\""
            resValue "string", "ad_id_workout_start", "\"ca-app-pub-5961771507160254/9362730913\""
            resValue "string", "ad_id_workout_end", "\"ca-app-pub-5961771507160254/3368168205\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.2'
    }

    applicationVariants.all { variant ->
        variant.addJavaSourceFoldersToModel(
                new File(buildDir, "generated/ksp/${variant.name}/kotlin")
        )
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation platform('androidx.compose:compose-bom:2023.06.01')
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation "androidx.compose.material:material-icons-extended:1.6.3"
    implementation 'androidx.compose.ui:ui-viewbinding'
    implementation 'androidx.compose.ui:ui-graphics-android:1.6.3'


    // Preview not working without these
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Compose Navigation
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation 'io.github.raamcosta.compose-destinations:core:1.9.42-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.9.42-beta'


    // Hilt DI
    implementation "com.google.dagger:hilt-android:2.49"
    ksp "com.google.dagger:hilt-compiler:2.47"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"

    implementation "androidx.preference:preference-ktx:1.2.1"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.google.android.play:review-ktx:2.0.1'

    implementation 'com.chargemap.compose:numberpicker:1.0.3'


    // Room
    implementation 'androidx.room:room-common:2.6.1'
    implementation "androidx.room:room-ktx:2.6.1"
    implementation "androidx.room:room-runtime:2.6.1"
    ksp "androidx.room:room-compiler:2.6.1"

    // Play Services - using BoM
    implementation platform('com.google.firebase:firebase-bom:32.2.3')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.android.gms:play-services-ads:23.0.0'

    // Monetization
    implementation 'com.android.billingclient:billing-ktx:6.2.0'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation("com.google.android.ump:user-messaging-platform:2.2.0")

    // Testing
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1"
}